buildscript {
    ext {
        kotlinVersion = '0.14.449'
        domaVersion = '2.4.1'
        buriteriDestDir = 'src/gen/kotlin'
        buriteriSrcDir = 'src/main/buriteri'
        templatePackage = 'pages'
    }
    repositories.jcenter()
    dependencies {
        classpath group:'org.jetbrains.kotlin', name:'kotlin-gradle-plugin', version:kotlinVersion
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'io.github.drmashu'
version = '0.1-SNAPSHOT'

description = """"""

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
}

configurations {
    buriteri
}

task precompileBuriteri << {
    ant.taskdef(name: 'buriteri',
            classname: 'io.github.drmashu.buri.template.BuriTeriTask',
            classpath: configurations.buriteri.asPath)
    ant.buriteri(srcdir: buriteriSrcDir,
            destdir: buriteriDestDir,
            packagename: templatePackage)
}
compileKotlin.dependsOn precompileBuriteri
compileJava.dependsOn processResources
processResources.destinationDir = compileJava.destinationDir
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
    main.kotlin.srcDirs += buriteriDestDir
}

dependencies {
    compile group: 'junit', name: 'junit', version:'4.12'
    compile group: 'io.github.drmashu', name: 'buri', version:'0.1-SNAPSHOT'
    compile group: 'com.h2database', name:'h2', version:'1.4.189'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version:kotlinVersion
    compile group: 'org.seasar.doma', name: 'doma', version:domaVersion
    kapt group: 'org.seasar.doma', name: 'doma', version:domaVersion
    testCompile group: 'junit', name: 'junit', version: '4.11'
    buriteri group: 'io.github.drmashu', name: 'buriteri', version:'0.1-SNAPSHOT'
}

